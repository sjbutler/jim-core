/*
 * 
 */

apply plugin: 'java-library'
apply plugin: 'antlr'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "uk.org.facetus"
archivesBaseName = "jim-core"
version = "0.0.5"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    antlr 'org.antlr:antlr4:4.7.1'
    
    compile 'org.antlr:antlr4-runtime:4.7.1'
    compile 'uk.org.facetus:idtk-j:0.5.2'
    compile 'uk.org.facetus:intt:0.8.3'
    compile 'org.apache.commons:commons-collections4:4.1'
    
    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

compileJava {
    options.compilerArgs << '-Xlint:deprecation'
}

generateGrammarSource {
    arguments += ["-visitor", "-long-messages"]
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
